config {
  type: "incremental",
  uniqueKey: ["id_viagem"],
  schema: "monitoramento_staging",
  name: "gps_viagem",
  tags: ["monitoramento_viagem"],
  bigquery: {
    partitionBy: "data"
  }
}

with
    viagem as (
        select
            data,
            id_viagem,
            datetime_partida,
            datetime_chegada,
            modo,
            id_veiculo,
            trip_id,
            route_id,
            shape_id,
            servico,
            sentido,
            fonte_gps
        from ${ref("viagem_informada")}
        ${when(
            incremental(),
            `where data between date(${constants.date_range_start}) and date(${constants.date_range_end})`
        )} 
    ),
    gps_conecta as (
        select 
            data, 
            timestamp_gps, 
            servico, 
            id_veiculo, 
            latitude, 
            longitude
        from ${ref("br_rj_riodejaneiro_veiculos", "gps_sppo")}
        where
        (
        ${when(
            incremental(),
            `data between date_sub(date(${constants.date_range_start}), interval 1 day) 
                and date_add(date(${constants.date_range_end}), interval 1 day)`,
            `data >= date(${helpers.data_inicial_gps_validacao_viagem})`
        )}
        )
    ),
    gps_zirix as (
        select 
            data, 
            timestamp_gps, 
            servico, 
            id_veiculo, 
            latitude, 
            longitude
        from ${ref("br_rj_riodejaneiro_onibus_gps_zirix", "gps_sppo")}
        where
        (
        ${when(
            incremental(),
            `data between date_sub(date(${constants.date_range_start}), interval 1 day) 
                and date_add(date(${constants.date_range_end}), interval 1 day)`,
            `data >= date(${helpers.data_inicial_gps_validacao_viagem})`
        )}
        )
    ),
    gps_union as (
        select *, 'conecta' as fornecedor
        from gps_conecta

        union all

        select *, 'zirix' as fornecedor
        from gps_zirix
    )
select
    v.data,
    g.timestamp_gps,
    v.modo,
    g.id_veiculo,
    g.servico,
    v.sentido,
    g.latitude,
    g.longitude,
    st_geogpoint(g.longitude, g.latitude) as geo_point_gps,
    v.id_viagem,
    v.datetime_partida,
    v.datetime_chegada,
    v.trip_id,
    v.route_id,
    v.shape_id,
    v.fonte_gps,
    ${constants.versao} as versao,
    current_datetime("America/Sao_Paulo") as datetime_ultima_atualizacao
from gps_union g
join
    viagem v
    on g.timestamp_gps between v.datetime_partida and v.datetime_chegada
    and g.id_veiculo = v.id_veiculo
    and g.servico = v.servico
    and g.fornecedor = v.fonte_gps
${when(
    incremental(),
    '',
    `where v.data <= date_sub(current_date("America/Sao_Paulo"), interval 2 day)`
)}
